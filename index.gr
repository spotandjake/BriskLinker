// Imports
import Bytes from "bytes"
import File from "sys/file"
import Result from "result"
import Int64 from "int64"
import String from "string"
import { link } from "./linker/index"
// Definition For Write File
let writeFile = (filePath, fileContents) => {
  // TODO: if the file already exists this does not work
  // Open The File
  let fd = File.pathOpen(
    File.pwdfd,
    [File.SymlinkFollow],
    filePath,
    [File.Create],
    [File.FdWrite],
    [File.FdWrite],
    []
  )
  // Read The File Contents
  match (fd) {
    Ok(descriptor) => {
      // Write The File Contents
      let writeSuccess = File.fdWrite(descriptor, fileContents)
      match (writeSuccess) {
        Ok(_) => void,
        Err(error) => fail "[Error Writing File: " ++ filePath ++ "]",
      }
    },
    Err(error) => fail "[Error: " ++ filePath ++ "]",
  }
}
// Definition For ReadFile
let readFile = filePath => {
  // Open The File
  let fd = File.pathOpen(
    File.pwdfd,
    [File.SymlinkFollow],
    filePath,
    [File.Exclusive],
    [File.FdRead, File.FdFilestats],
    [File.FdRead],
    []
  )
  // Read The File Contents
  match (fd) {
    Ok(descriptor) => {
      let stats = Result.unwrap(File.fdFilestats(descriptor))
      let (contents, _) = Result.unwrap(
        File.fdRead(descriptor, Int64.toNumber(stats.size))
      )
      Ok(Bytes.fromString(contents))
    },
    Err(error) => Err("[Error: " ++ filePath ++ "]"),
  }
}
// Definition For pathAbsolute
let pathAbsolute = (parentPath, filePath) => {
  // TODO: Develop A Nice Module For Handling File Paths
  // TODO: Normalize The File Path because currently this will not work well
  let fileNamePos = match (String.indexOf("/", String.reverse(parentPath))) {
    Some(pos) => pos,
    None => 0,
  }
  // Remove The File Name From The ParentPath
  let parentPath = String.slice(
    0,
    String.length(parentPath) - fileNamePos,
    parentPath
  )
  // TODO: Resolve Path
  parentPath ++ filePath
}
// Perform Linking
let linkBuild = (filePath, outputPath) => {
  // Link The File
  let wasmOutput = link(filePath, readFile, pathAbsolute)
  // Write The output File
  writeFile(outputPath, wasmOutput)
}

// link The Build
linkBuild("./LinkerTest/index.br.wasm", "./LinkerTest/Linker_Linked.br.wasm")
