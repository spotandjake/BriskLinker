module Stream

// Imports
include "runtime/unsafe/wasmi32"
include "runtime/numbers"
include "bytes"
include "list"
from Numbers use { coerceNumberToWasmI32 }
// Helpers
@unsafe
let tagSimple = x => WasmI32.xor(WasmI32.shl(x, 1n), 1n)
// Library For Interacting With Streams Of Data
provide record Stream {
  data: Bytes,
  mut position: Number,
}
// Functions
provide let empty = () => {
  { data: Bytes.empty, position: 0 }
}
provide let fromBytes = data => {
  // Create A New Stream
  { data, position: 0 }
}
// Get Information
provide let isEmpty = stream => {
  Bytes.length(stream.data) == 0
}
// Get The Next Element
@unsafe
provide let nextByte = stream => {
  // Get The Value
  let ptr = WasmI32.fromGrain(stream.data)
  let bytesOffset = WasmI32.add(ptr, 8n)
  let byte = WasmI32.load8U(bytesOffset, coerceNumberToWasmI32(stream.position))
  // Increment The Position
  stream.position += 1
  // Return The Value
  WasmI32.toGrain(tagSimple(byte)): Number
}
provide let nextUIntLEB128 = stream => {
  let mut result = 0
  let mut shift = 0
  while (true) {
    let byte = nextByte(stream)
    result = result | (byte & 0x7f) << shift
    if ((0x80 & byte) == 0) break
    shift += 7
  }
  // Return Result
  result
}
// provide let nextIntLEB128 = (stream) => {
//   void
// }
provide let nextString = stream => {
  // Get String Length
  let length = nextUIntLEB128(stream)
  // Get The String
  let str = Bytes.slice(stream.position, length, stream.data)
  let str = Bytes.toString(str)
  // Increment The Position
  stream.position += length
  // Return The String
  str
}
// Length
provide let length = stream => Bytes.length(stream.data)
provide let remainingLength = stream => length(stream) - stream.position
// Get The Next Slice
provide let nextSliceBytes = (stream, length) => {
  let result = Bytes.slice(stream.position, length, stream.data)
  stream.position += length
  result
}
provide let nextStreamSlice = (stream, length) => {
  let result = nextSliceBytes(stream, length)
  fromBytes(result)
}
provide let nextSliceList = (stream, length) => {
  List.init(length, index => nextByte(stream))
}
provide let remainingBytes = stream => {
  Bytes.slice(stream.position, remainingLength(stream), stream.data)
}
// Move The Position
provide let advance = (stream, length) => {
  stream.position += length
}
// Reset
provide let resetPointer = stream => {
  stream.position = 0
  stream
}
